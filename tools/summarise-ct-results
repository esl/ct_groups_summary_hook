#!/usr/bin/env escript

main(Directories) ->
    {OkTests, FailedTests, UserSkipped, AutoSkipped} =
        add_up_suite_results(suite_summaries(Directories)),
    {OkGroups, FailedGroups, EventuallyOkTests, EndPerTestcaseFailed} =
        add_up_group_results(group_summaries(Directories)),
    verify_results(OkGroups, FailedGroups, EventuallyOkTests, EndPerTestcaseFailed,
                   OkTests, FailedTests, UserSkipped, AutoSkipped).

verify_results(0 = _OkGroups, 0 = _FailedGroups, 0 = _EventuallyOkTests, 0 = _EndPerTestcaseFailed,
               OkTests, FailedTests, UserSkipped, AutoSkipped) ->
    %% These are likely small tests - we're not writing
    %% groups.summaries files, therefore we get 0 for both group kinds.
    verify_results_no_groups(OkTests, FailedTests, UserSkipped, AutoSkipped);
verify_results(OkGroups, FailedGroups, EventuallyOkTests, EndPerTestcaseFailed,
               OkTests, FailedTests, UserSkipped, AutoSkipped) ->
    verify_results_with_groups(OkGroups, FailedGroups, EventuallyOkTests, EndPerTestcaseFailed,
                               OkTests, FailedTests, UserSkipped, AutoSkipped).

verify_results_no_groups(OkTests, FailedTests, UserSkipped, AutoSkipped) ->
    io:format("CT results:~n"
              "    ~p tests passed~n"
              "    ~p tests failed~n"
              "    ~p tests skipped by user~n"
              "    ~p tests skipped automatically~n",
              [OkTests, FailedTests, UserSkipped, AutoSkipped]),
    case OkTests =< 0 of
        true ->
            erlang:halt(100404);
        _ ->
            erlang:halt(FailedTests + AutoSkipped)
    end.

verify_results_with_groups(OkGroups, FailedGroups, EventuallyOkTests, EndPerTestcaseFailed,
                           OkTests, FailedTests, UserSkipped, AutoSkipped) ->
    io:format("CT results:~n"
              "    ~p groups passed~n"
              "    ~p groups failed~n"
              "    ~p tests eventually passed~n"
              "    ~p tests with end_per_testcase failed~n"
              "    ~p tests passed~n"
              "    ~p tests failed~n"
              "    ~p tests skipped by user~n"
              "    ~p tests skipped automatically~n",
              [OkGroups, FailedGroups, EventuallyOkTests, EndPerTestcaseFailed,
               OkTests, FailedTests, UserSkipped, AutoSkipped]),
    Code = FailedGroups + FailedTests - EventuallyOkTests + EndPerTestcaseFailed + AutoSkipped,
    if
        OkTests =:= 0 ->
            io:format("No tests were executed~n");
        OkGroups =:= 0 ->
            io:format("No test groups were executed~n");
        AutoSkipped > 0 ->
            %% this can give false negative result for eventually passing
            %% groups with 'sequence' and 'repeat_until_all_ok' flags, but
            %% at least it's not false positive result.
            io:format("Failing the test due to auto skipped cases~n");
        FailedGroups > 0 ->
            io:format("Failing the test due to failed rerun groups~n");
        FailedTests > EventuallyOkTests ->
            io:format("Failing the test due to failed test cases~n");
        EndPerTestcaseFailed > 0 ->
            io:format("Failing the test due to failed end_per_testcase~n");
        true ->
            ok
    end,
    if
        OkTests =:= 0 ->
            erlang:halt(100404);
        OkGroups =:= 0 ->
            erlang:halt(100405);
        true ->
            erlang:halt(Code)
    end.

suite_summaries(Directories) ->
    summaries(Directories, "suite.summary").

group_summaries(Directories) ->
    summaries(Directories, "groups.summary").

summaries(Directories, Filename) ->
    lists:foldl(fun(Dir, Acc) ->
                        Wildcard = filename:join([Dir, "*.logs", "*", Filename]),
                        Acc ++ filelib:wildcard(Wildcard)
                end, [], Directories).

add_up_suite_results(SuiteSummaries) ->
    lists:foldl(fun(Filename, {OkAcc, FAcc, USAcc, ASAcc}) ->
                        {ok, [{summary, Summary}]} = file:consult(Filename),
                        [Ok, F, US, AS | _] = tuple_to_list(Summary),
                        {OkAcc + Ok, FAcc + F, USAcc + US, ASAcc + AS}
                end, {0, 0, 0, 0}, SuiteSummaries).

add_up_group_results(GroupSummaries) ->
    lists:foldl(fun(Filename, {OkAcc, FAcc, EvOkAcc, EndPerTestcaseFailedAcc}) ->
                        {ok, Terms} = file:consult(Filename),
                        #{eventually_ok_tests := EvOk,
                          end_per_testcase_failures := EndPerTestcaseFailed,
                          groups_summary := {Ok, F}}
                            = maps:from_list(Terms),
                        {OkAcc + Ok, FAcc + F, EvOkAcc + EvOk, EndPerTestcaseFailedAcc + EndPerTestcaseFailed}
                end, {0, 0, 0, 0}, GroupSummaries).
